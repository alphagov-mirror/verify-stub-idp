idea {
    module {
        // add the integration tests to the idea test sources for convenience
        // this makes idea different to command line config as on the command line
        // where there are entirely separate source sets for unit and integration tests
        testSourceDirs += file('stub-idp/src/integration-test/java')
        testSourceDirs += file('stub-idp/src/integration-test/resources')
    }
}

sourceSets {
    integrationTest {
        java {
            srcDir "${rootDir}/stub-idp/src/integration-test/java"
        }
        resources {
            srcDir "${rootDir}/stub-idp/src/integration-test/resources"
            srcDir "${rootDir}/configuration"
        }
        compileClasspath += sourceSets.main.runtimeClasspath
        compileClasspath += sourceSets.test.runtimeClasspath
    }
}

task intTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath += sourceSets.integrationTest.runtimeClasspath
    reports.junitXml.destination = file("${project.buildDir}/int-test-results")
    reports.html.destination = file("${project.buildDir}/int-test-reports")
    workingDir = rootDir
    include 'uk/gov/ida/**'
}

